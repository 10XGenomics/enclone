#!/bin/csh
#
# Find the difference between the results of two enclone runs, assuming that the difference
# can be obtained by adding a parameter to the first run.
#
# See also diff_runs2.
#
# USAGE:
# ./diff_runs dataset-specification old_param <up to four optional args for all> | less -R
#
# EXAMPLE:
# ./diff_runs BCR=123085 NDOUBLET | less -R
#
# LIMITATIONS:
# - makes some temporary files
# - makes some imperfect assumptions to do the diff
# - fails inscrutably if there is no change

set dataset_spec = $1
set old_param = $2
set all = "$3 $4 $5 $6"

set pcols = "barcode,exact_subclonotype_id,nchains"

enclone POUT=~/old PCELL PCOLS=$pcols NOPRINT $dataset_spec $old_param $all > /dev/null
cat ~/old | sort > ~/olds

enclone POUT=~/new PCELL PCOLS=$pcols NOPRINT $dataset_spec $all
cat ~/new | sort > ~/news

set bcs = `diff ~/olds ~/news | grep "<" | tr ',' ' ' | awk '{ print $2 }' \
    | tr '\n' ' ' | sed 's/.$//'`

foreach barcode ($bcs)
    echo OLD
    enclone $dataset_spec BARCODE=$barcode $old_param $all NOPAGER
    echo NEW
    enclone $dataset_spec BARCODE=$barcode $all NOPAGER
end
