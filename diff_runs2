#!/bin/csh
#
# Find the difference between the results of two enclone runs, assuming that one run is given
# by using the current binary and the other run is given by the binary in ~/bin.
#
# See also diff_runs.
#
# USAGE:
# ./diff_runs2 dataset-specification <up to four optional args for all> | less -R
#
# EXAMPLE:
# diff_runs2 BCR=123085 | less -R
#
# LIMITATIONS:
# - makes some temporary files
# - makes some imperfect assumptions to do the diff
# - fails inscrutably if there is no change

set dataset_spec = $1
set all = "$2 $3 $4 $5"

set pcols = "barcode,exact_subclonotype_id,nchains"

~/bin/enclone POUT=~/old PCELL PCOLS=$pcols NOPRINT $dataset_spec $all > /dev/null
cat ~/old | sort > ~/olds

enclone POUT=~/new PCELL PCOLS=$pcols NOPRINT $dataset_spec $all
cat ~/new | sort > ~/news

set bcs = `diff ~/olds ~/news | grep "<" | tr ',' ' ' | awk '{ print $2 }' \
    | tr '\n' ' ' | sed 's/.$//'`

foreach barcode ($bcs)
    echo OLD
    ~/bin/enclone $dataset_spec BARCODE=$barcode $all NOPAGER
    echo NEW
    enclone $dataset_spec BARCODE=$barcode $all NOPAGER
end
